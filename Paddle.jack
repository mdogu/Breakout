/**
 * Represents a paddle.
 */
class Paddle {

    field int x, y;
    field int width, height;
    field int dx;

    /** Constructs a new paddle with given coordinates and size */
    constructor Paddle new(int Ax, int Ay, int Awidth, int Aheight, int Adx) {
        let x = Ax;
        let y = Ay;
        let width = Awidth;
        let height = Aheight;
        let dx = Adx;
        do show();
        return this;
    }

    /** Getter & Setters */

    method int getX() { return x; }
    method void setX(int Ax) { let x = Ax; return; }

    method int getY() { return y; }
    method void setY(int Ay) { let y = Ay; return; }

    method int getDx() { return dx; }
    method void setDx(int Adx) { let dx = Adx; return; }

    method int getWidth() { return width; }
    method void setWidth(int Awidth) { let width = Awidth; return; }

    /** Deallocates the object's memory. */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    /** Shows the paddle. */
    method void show() {
        do Screen.setColor(true);
        do draw();
        return;
    }

    /** Hides the paddle. */
    method void hide() {
        do Screen.setColor(false);
        do draw();
        return;
    }

    /** Draws the paddle. */
    method void draw() {
        do Screen.drawRectangle(x, y, x + width, y + height);
        return;
    }

    /** Moves the paddle to right */
    method void moveRight() {
        do hide();
        let x = x + dx;
        do show();
        return;
    }

    /** Moves the paddle to left */
    method void moveLeft() {
        do hide();
        let x = x - dx;
        do show();
        return;
    }

}